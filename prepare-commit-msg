#!/usr/bin/perl -p
#
# Copyright (C) 2016 Andrew Ayer
#
# See COPYING file for license information.
#

use strict;
use warnings;

our @changed_zones;

BEGIN {
	die "Bad arguments. Is this being called from git?\n" unless @ARGV >= 1;
	$^I = '';
	@ARGV = ($ARGV[0]);

	my %changed_zones;
	foreach my $line (`git status --porcelain`) {
		chomp $line;
		if ($line =~ m{^[MADRCU]. db\.([^/]+)}) {
			$changed_zones{$1} = 1;
		}
		if ($line =~ m{^[MADRCU]. .+ -> db\.([^/]+)}) {
			$changed_zones{$1} = 1;
		}
	}
	@changed_zones = sort keys %changed_zones;
	exit 0 unless @changed_zones;
}

if ($. == 1) {
	print "[" . join(', ', @changed_zones) . "] " unless /^\[/;
}
